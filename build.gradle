import com.github.spotbugs.snom.Effort


buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
        maven {
            url = "http://localhost:8081/artifactory/libs-release-local"
            allowInsecureProtocol = true
            credentials {
                username = "${artifactory_user}"
                password = "${artifactory_password}"
            }
        }
    }

    dependencies {
        //Check for the latest version here: http://plugins.gradle.org/plugin/com.jfrog.artifactory
        classpath("org.jfrog.buildinfo:build-info-extractor-gradle:6.+")
    }
}

plugins {
    id "com.github.spotbugs" version "6.2.5"
    id 'java'
    id 'idea'
    id 'maven-publish'
    id 'jacoco'
    id "com.jfrog.artifactory" version "6.+"
    id 'signing'
}

// set the java version
java {
    sourceCompatibility = JavaVersion.VERSION_25
    targetCompatibility = JavaVersion.VERSION_25
    withJavadocJar()
    withSourcesJar()
}

group = 'com.codingrodent.microprocessor'

def buildNum = System.getenv("BUILD_NUMBER")
version = buildNum
        ? "${projectVersionMajor}.${projectVersionMinor}.${buildNum}"
        : "${projectVersionMajor}.${projectVersionMinor}.${projectVersionBuild}"

jar {
    archiveBaseName = projectName
    manifest
            {
                attributes 'Implementation-Title': projectName,
                        'Implementation-Version': archiveVersion
            }
}

javadoc {
    options.addBooleanOption('html5', true)
}

repositories {
    mavenCentral()
}

jacoco {
    toolVersion = "0.8.14"
}

test {
    useJUnitPlatform()
    maxParallelForks = 1
    filter
            {
                includeTestsMatching "com.codingrodent.microprocessor.*"
            }
    jacoco {
        destinationFile = layout.buildDirectory.file("jacoco/jacocoTest.exec").get().asFile
        classDumpDir = layout.buildDirectory.dir("jacoco/classpathdumps").get().asFile
    }
}

jacocoTestReport {
    reports {
        xml.required = true
        csv.required = false
        html.outputLocation = layout.buildDirectory.dir("jacocoHtml")
    }
}

spotbugs {
    toolVersion = "4.9.8"
    ignoreFailures = false
    showProgress = true
    effort = Effort.MAX
    //  reportLevel = Confidence.HIGH
    excludeFilter = file("$rootProject.projectDir/excludeFilter.xml")
}

spotbugsMain {
    reports.create("html") {
        required = true
    }
}

artifactory {
    contextUrl = "${artifactory_contextUrl}"   //The base Artifactory URL if not overridden by the publisher/resolver
    publish {
        repository {
            repoKey = 'libs-release-local'
            username = "${artifactory_user}"
            password = "${artifactory_password}"
        }
        defaults {
            // Reference to Gradle publications defined in the build script.
            // This is how we tell the Artifactory Plugin which artifacts should be
            // published to Artifactory.
            publications('mavenJava')
            publishArtifacts = true
            // Properties to be attached to the published artifacts.
            properties = ['qa.level': 'basic', 'dev.team': 'core']
            // Publish generated POM files to Artifactory (true by default)
            publishPom = true
        }
    }
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                artifactId = 'Z80Processor'
                groupId = 'com.codingrodent.microprocessor'
                name = 'com.codingrodent.microprocessor.Z80Processor'
                description = 'A Z80 Microprocessor core in Java'
                url = 'https://github.com/codesqueak/Z80Processor'
                scm {
                    url = 'https://github.com/codesqueak/Z80Processor'
                    connection = 'scm:git:git://github.com/codesqueak/Z80Processor.git'
                    developerConnection = 'scm:git:ssh://github.com:codesqueak/Z80Processor.git'
                }
                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'codesqueak'
                        name = 'codesqueak'
                        organizationUrl = 'http://www.codesqueak.com'
                        //  organization 'codesqueak'
                    }
                }
            }
        }
    }
}

signing {
    sign(publishing.publications["mavenJava"])
}

// check.dependsOn jacocoTestReport

dependencies {
    implementation("com.github.spotbugs:spotbugs-annotations:4.9.8")
    //
    testImplementation("org.junit.jupiter:junit-jupiter-api:6.0.1")
    testImplementation("org.junit.jupiter:junit-jupiter-engine:6.0.1")
    testImplementation("org.junit.platform:junit-platform-launcher:6.0.1")
    testImplementation("org.mockito:mockito-core:5.20.0")
    testImplementation("org.hamcrest:hamcrest:3.0")
}

wrapper {
    gradleVersion = '9.2.1'
    distributionType = Wrapper.DistributionType.BIN
}
